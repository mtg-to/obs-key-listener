; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "OBS Key Listener"
#define MyAppVersion "0.1"
#define MyAppPublisher "Piotr Berlowski"
#define MyAppURL "https://github.com/mtg-to/obs-key-listener"
#define MyAppExeName "listener.exe"

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{18B7B98D-8AAA-4488-B8DE-CB88821940D4}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={autopf}\{#MyAppName}
DisableProgramGroupPage=yes
InfoAfterFile="{#SourcePath}\user-info.txt"
LicenseFile="{#SourcePath}\..\LICENSE.txt"
; Uncomment the following line to run in non administrative install mode (install for current user only.)
;PrivilegesRequired=lowest
OutputDir=D:\workspace\mtg-to\obs-key-listener\innosetup
OutputBaseFilename=obs-key-listener-setup
Compression=lzma
SolidCompression=yes
WizardStyle=modern

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
Source: "{#SourcePath}\..\dist\listener.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#SourcePath}\..\config\*"; DestDir: "{commonappdata}\{#MyAppName}"; Flags: ignoreversion recursesubdirs createallsubdirs
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{autoprograms}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; WorkingDir: "{commonappdata}\{#MyAppName}"; Parameters: "-c default_bindings.yml -o obs.txt -f {code:SelectedFormat}";
Name: "{autodesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; WorkingDir: "{commonappdata}\{#MyAppName}"; Parameters: "-c default_bindings.yml -o obs.txt -f {code:SelectedFormat}"; Tasks: desktopicon

[Code]
var
  FormatOptionWizardPage: TInputOptionWizardPage;
  Formats: Array of String;

procedure InitializeWizard;
var
  i: byte;
begin
  Formats := ['edh', 'constructed', 'simple'];
  FormatOptionWizardPage := CreateInputOptionPage(wpSelectTasks, 'Choose your default format:', 'Choose the format you play from your default config', 'Format', True, False);
  for i:= 0 to 2 do
  begin
    FormatOptionWizardPage.Add(Formats[i]);
  end;
  FormatOptionWizardPage.Values[0] := True;
end;

function SelectedFormat(Param: String): String;
begin
  Result := Formats[FormatOptionWizardPage.SelectedValueIndex];
end;